plugins {
    id 'java'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.22.0'
    id 'io.freefair.lombok' version '8.4'
}

version = '1.0.0'
group = 'org.cecil'

repositories {
    mavenCentral()
}

def integrationTest = sourceSets.create('integrationTest')

configurations[integrationTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)
configurations[integrationTest.runtimeOnlyConfigurationName].extendsFrom(configurations.testRuntimeOnly)

tasks.named('test') {
    useJUnitPlatform()
}

def integrationTestTask = tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    useJUnitPlatform()

    testClassesDirs = integrationTest.output.classesDirs
    classpath = configurations[integrationTest.runtimeClasspathConfigurationName] + integrationTest.output

    shouldRunAfter(tasks.named('test'))
}

tasks.named('check') {
    dependsOn(integrationTestTask)
}


tasks.register('validate') {
    dependsOn 'checkstyleMain', 'checkstyleTest', 'spotlessCheck'
}

checkstyle {
    toolVersion = '10.12.5'
    configFile = file('config/checkstyle/checkstyle.xml')
}

spotless {
    java {
        googleJavaFormat()
    }
    kotlin {
        ktfmt()
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(20)
    }
    withSourcesJar()
    withJavadocJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    integrationTestImplementation project
}
